apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

apply from: 'config.gradle'

def config = project.ext

group = config.pkg.groupId                                         // 数据仓库依赖第一部分
version = config.pkg.version                                       // 数据仓库依赖第三部分

install {
    repositories.mavenInstaller {
        pom {
            project {
                groupId config.pkg.groupId
                artifactId config.pkg.artifactId
                version config.pkg.version
                packaging 'aar'
                name project.name                           // 项目名称
                url config.cvs.url
                description config.pkg.desc

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id config.developer.id                     // 开发者信息
                        name config.developer.name                 // 开发者信息
                        email config.developer.email               // 开发者信息
                    }
                }
                scm {
                    connection "${config.cvs.url}.git"
                    developerConnection "${config.cvs.url}.git"
                    url config.cvs.url
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
        author true
        version true
        links 'http://docs.oracle.com/javase/8/docs/api'
        title config.doc.title                                     // 文档标题
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// 生成jar包
task releaseJar(type: Copy) {
    from('build/intermediates/bundles/default')

    File jarDir = file("../jar")

    if (file("../jar").exists()) {
        jarDir.deleteDir()
    }
    jarDir.mkdirs()
    into('../jar')
    include('classes.jar')
    rename('classes.jar', project.name + '-' + config.pkg.version + '.jar')
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    dryRun = false
    override = false

    pkg {
        repo = "maven"
        name = config.pkg.artifactId                                   // 数据仓库依赖第二部分
        userOrg = config.user.org
        desc = config.pkg.desc
        websiteUrl = config.cvs.url
        issueTrackerUrl = "${config.cvs.url}/issues"
        vcsUrl = "${config.cvs.url}.git"
        licenses = ["Apache-2.0"]
        version {
            name = config.pkg.version
            desc = config.pkg.desc
            released = new Date()
            vcsTag = config.pkg.version
            attributes = [
                    'android-maven-gradle-plugin': 'https://github.com/dcendents/android-maven-gradle-plugin',
                    'gradle-bintray-plugin'      : 'https://github.com/bintray/gradle-bintray-plugin'
            ]
        }
    }
}